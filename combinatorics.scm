(define algorithm-P
  (lambda (A visit)
    (define n (vector-length A))
    (define j)
    (define s)
    (define q)
    (define C (make-vector (1+ n) 0))
    (define O (make-vector (1+ n) 1))
    (define (P2)
      (visit A)
      (P3))
    (define (P3)
      (set! j n)
      (set! s 0)
      (P4))
    (define (P4)
      (set! q (+ (vector-ref O j)
		 (vector-ref C j)))
      (cond ((< q 0) (P7))
	    ((= q j) (P6))
	    (else (P5))))
    (define (P5)
      (vector-swap! A
		    (+ j s (- (vector-ref C j)) -1)
		    (+ j s (- q) -1))
      (vector-set! C j q)
      (P2))
    (define (P6)
      (unless (= j 1)
	(inc! s)
	(P7)))
    (define (P7)
      (vector-modify! O j -)
      (dec! j)
      (P4))
    (P2)))

